open Common
open Containers
open Value
open Ops

module AbstractExpr = struct
  type ('const, 'var, 'unary, 'binary, 'intrin, 'e) t =
    | RVar of 'var  (** variables *)
    | Constant of 'const
        (** constants; a pure intrinsic function with zero arguments *)
    | UnaryExpr of 'unary * 'e
        (** application of a pure intrinsic function with one arguments *)
    | BinaryExpr of 'binary * 'e * 'e
        (** application of a pure intrinsic function with two arguments *)
    | ApplyIntrin of 'intrin * 'e list
        (** application of a pure intrinsic function with n arguments *)
    | ApplyFun of string * 'e list
        (** application of a pure runtime-defined function with n arguments *)
    | Binding of 'e list * 'e  (** syntactic binding in a nested scope *)
  [@@deriving eq, ord, fold, map, iter]

  let id a b = a
  let fold f b o = fold id id id id id f b o

  let map f e =
    let id a = a in
    map id id id id id f e

  let hash hash e1 =
    let hash_const = Hashtbl.hash in
    let hash_var = Hashtbl.hash in
    let hash_unary = Hashtbl.hash in
    let hash_binary = Hashtbl.hash in
    let hash_intrin = Hashtbl.hash in
    let open HashHelper in
    match e1 with
    | RVar r -> combine 1 (hash_var r)
    | UnaryExpr (op, a) -> combine2 3 (hash_unary op) (hash a)
    | BinaryExpr (op, l, r) -> combine3 5 (hash_binary op) (hash l) (hash r)
    | Constant c -> combine 7 (hash_const c)
    | ApplyIntrin (i, args) ->
        combine 11 (combinel (hash_intrin i) (List.map hash args))
    | ApplyFun (n, args) ->
        combine 13 (combinel (String.hash n) (List.map hash args))
    | Binding (args, body) ->
        combine 17 (combinel (hash body) (List.map hash_var args))
end

module Alges = struct
  open AbstractExpr

  let children_alg a =
    let alg a = fold (fun acc a -> a @ acc) [] a in
    alg

  let hash_alg (hash_const : 'a -> int) (hash_var : 'b -> int) =
    let alg a =
      match a with
      | RVar v -> hash_var v
      | Constant c -> hash_const c
      | UnaryExpr (op, e) -> Hash.pair Hashtbl.hash Fun.id (Hashtbl.hash op, e)
      | BinaryExpr (op, e, e2) ->
          Hash.triple Hashtbl.hash Fun.id Fun.id (Hashtbl.hash op, e, e2)
      | ApplyIntrin (op, es) ->
          Hash.pair Hashtbl.hash (Hash.list Fun.id) (op, es)
      | ApplyFun (n, es) -> Hash.pair Hash.string (Hash.list Fun.id) (n, es)
      | Binding (vs, b) -> Hash.pair (Hash.list hash_var) Fun.id (vs, b)
    in
    alg
end

module type Fix = sig
  type const
  type var
  type unary
  type binary
  type intrin
  type t

  module Var : HASH_TYPE with type t = var

  val fix : (const, var, unary, binary, intrin, t) AbstractExpr.t -> t
  val unfix : t -> (const, var, unary, binary, intrin, t) AbstractExpr.t
end

module Recursion (O : Fix) = struct
  open Fun.Infix
  open O

  type 'e abstract_expr =
    (const, Var.t, unary, binary, intrin, 'e) AbstractExpr.t

  (** Recursion schemes on AbstractExpr.t for a given fix point type.

      See:

      https://arxiv.org/pdf/2202.13633v1

      https://icfp24.sigplan.org/details/ocaml-2024-papers/11/Recursion-schemes-in-OCaml-An-experience-report
  *)

  (** Fold an algebra e -> 'a through the expression, from leaves to nodes to
      return 'a. *)
  let rec cata alg e = (unfix %> AbstractExpr.map (cata alg) %> alg) e

  (* ana coalg = Out◦ ◦ fmap (ana coalg) ◦ coalg *)
  let rec ana coalg e = (coalg %> AbstractExpr.map (ana coalg) %> fix) e

  (** Apply function ~f to the expression first, then pass it to alg. Its result
      is accumulated down the expression tree. *)
  let rec map_fold ~f ~alg r e =
    let r = f r (unfix e) in
    (unfix %> (AbstractExpr.map (map_fold ~f ~alg r) %> alg r)) e

  (* hylo *)
  let rec hylo ~consume_alg ~produce_coalg e =
    consume_alg
    % AbstractExpr.map (hylo ~consume_alg ~produce_coalg)
    % produce_coalg
    @@ e

  (* mutual recursion:
    simultaneously evaluate two catamorphisms which can depend on each-other's results.
     *)
  let rec mutu alg1 alg2 =
    let alg x = (alg1 x, alg2 x) in
    (fst % cata alg, snd % cata alg)

  (* zygomorphism;

     Perform two recursion simultaneously, passing the result of the first to the second
     *)
  let rec zygo alg1 alg2 e = snd (mutu (AbstractExpr.map fst %> alg1) alg2) e

  (* zygo with the order swapped *)
  let rec zygo_l alg2 alg1 = fst (mutu alg1 (AbstractExpr.map snd %> alg2))

  let map_fold2 ~f ~alg1 ~alg2 r e =
    let alg r x = (alg1 r x, alg2 (AbstractExpr.map snd x)) in
    fst (fst % map_fold ~f ~alg r, snd % map_fold ~f ~alg r) e

  (** catamorphism that also passes the original expression through *)
  let rec para_f alg f e =
    let p f g x = (f x, g x) in
    (alg % AbstractExpr.map (p f (para_f alg f)) % unfix) e

  let para alg e = para_f alg identity e

  (**smart constructors **)

  let rvar v = fix (RVar v)
  let const v = fix (Constant v)
  let binexp ~op l r = fix (BinaryExpr (op, l, r))
  let unexp ~op arg = fix (UnaryExpr (op, arg))
  let fapply id params = fix (ApplyFun (id, params))
  let binding params body = fix (Binding (params, body))
  let applyintrin ~op params = fix (ApplyIntrin (op, params))
  let apply_fun ~name params = fix (ApplyFun (name, params))

  (** dont know *)
  let bind_match ~fconst ~frvar ~funi ~fbin ~fbind ~fintrin ~ffun
      (e : 'e abstract_expr) : 'a =
    let open AbstractExpr in
    match e with
    | RVar v -> frvar v
    | Constant c -> fconst c
    | UnaryExpr (op, e) -> funi op e
    | BinaryExpr (op, a, b) -> fbin op a b
    | Binding (a, b) -> fbind a b
    | ApplyIntrin (a, b) -> fintrin a b
    | ApplyFun (a, b) -> ffun a b

  (**helpers*)

  module VarSet = Set.Make (Var)

  (** get free vars of exprs *)
  let free_vars (e : t) =
    let open AbstractExpr in
    let alg e =
      match e with
      | RVar e -> VarSet.singleton e
      | Binding (b, e) ->
          VarSet.diff e (List.fold_left VarSet.union VarSet.empty b)
      | o -> fold (fun acc a -> VarSet.union a acc) VarSet.empty o
    in
    cata alg e

  (* substite variables for expressions *)
  let substitute (sub : var -> t option) (e : t) =
    let open AbstractExpr in
    let binding acc e =
      match e with
      | Binding (b, e) ->
          let v =
            List.map free_vars b |> List.fold_left VarSet.union VarSet.empty
          in
          VarSet.union acc v
      | o -> acc
    in
    let subst binding orig =
      match orig with
      | RVar e when VarSet.find_opt e binding |> Option.is_none -> (
          match sub e with Some r -> r | None -> fix orig)
      | o -> fix o
    in
    map_fold ~f:binding ~alg:subst VarSet.empty e

  (** get list of child expressions *)
  let children e = cata Alges.children_alg e
end

module BasilExpr = struct
  open AllOps

  module E = struct
    include AllOps

    type var = Var.t
    type 'a cell = 'a Fix.HashCons.cell

    let equal_cell _ a b = Fix.HashCons.equal a b
    let compare_cell _ a b = Fix.HashCons.compare a b

    type t = expr_node_v cell

    and expr_node_v =
      | E of (const, Var.t, unary, binary, intrin, t) AbstractExpr.t
    [@@deriving eq, ord]

    module HashExpr = struct
      type t = expr_node_v

      let hash e : int =
        e |> function E e -> AbstractExpr.hash Fix.HashCons.hash e

      let equal (i : t) (j : t) : bool =
        match (i, j) with
        | E i, E j ->
            AbstractExpr.equal AllOps.equal_const Var.equal AllOps.equal_unary
              AllOps.equal_binary AllOps.equal_intrin Fix.HashCons.equal i j
    end

    module H = Fix.HashCons.ForHashedTypeWeak (HashExpr)

    let fix i = H.make (E i)
    let unfix i = match Fix.HashCons.data i with E i -> i
  end

  include E

  module R = Recursion (struct
    include E
    module Var = Var
  end)

  include R

  (* printers *)
  let print_alg =
    let open AbstractExpr in
    function
    | RVar v -> Var.to_string v
    | Constant c -> AllOps.to_string c
    | UnaryExpr (op, e) -> AllOps.to_string op ^ "(" ^ e ^ ")"
    | BinaryExpr (op, e, e2) -> AllOps.to_string op ^ "(" ^ e ^ ", " ^ e2 ^ ")"
    | ApplyIntrin (op, es) ->
        AllOps.to_string op ^ "(" ^ String.concat ", " es ^ ")"
    | ApplyFun (n, es) -> n ^ "(" ^ String.concat ", " es ^ ")"
    | Binding (vs, b) -> String.concat " " vs ^ " :: " ^ b

  let to_string s = cata print_alg s
  let pp fmt s = Format.pp_print_string fmt @@ to_string s

  let type_alg (e : Types.BType.t abstract_expr) =
    let open AbstractExpr in
    let open Ops.AllOps in
    let get_ty o =
      match o with Fun { ret } -> ret | _ -> failwith "type error"
    in
    match e with
    | RVar r -> Var.typ r
    | Constant op -> ret_type_const op |> get_ty
    | UnaryExpr (op, a) -> ret_type_unary op a |> get_ty
    | BinaryExpr (op, l, r) -> ret_type_bin op l r |> get_ty
    | ApplyIntrin (op, args) -> ret_type_intrin op args |> get_ty
    | ApplyFun (a, b) -> Types.BType.Top
    | Binding (vars, b) -> Types.BType.uncurry vars b

  let type_of e = cata type_alg e
  let fold_with_type (alg : 'e abstract_expr -> 'a) = zygo_l type_alg alg

  (* constructor helpers *)
  let intconst (v : PrimInt.t) : t = const (`Integer v)
  let boolconst (v : bool) : t = const (`Bool v)
  let bvconst (v : PrimQFBV.t) : t = const (`Bitvector v)

  let bv_of_int ~(size : int) (v : int) : t =
    const (`Bitvector (PrimQFBV.of_int ~size v))

  let rewrite ~(rw_fun : t abstract_expr -> t option) (expr : t) =
    let rw_alg e =
      let orig s = fix s in
      match rw_fun e with Some e -> e | None -> orig e
    in
    cata rw_alg expr

  let rewrite_typed (f : (t * Types.BType.t) abstract_expr -> t option)
      (expr : t) =
    let rw_alg e =
      let orig s = fix @@ AbstractExpr.map fst s in
      match f e with Some e -> e | None -> orig e
    in
    fold_with_type rw_alg expr

  (** typed expression rewriter *)
  let rewrite_typed (f : (t * Types.BType.t) abstract_expr -> t option)
      (expr : t) =
    let rw_alg e =
      let orig s = fix @@ AbstractExpr.map fst s in
      match f e with Some e -> e | None -> orig e
    in
    fold_with_type rw_alg expr

  (** typed rewriter that expands two layers deep into the expression *)
  let rewrite_typed_two
      (f : (t abstract_expr * Types.BType.t) abstract_expr -> t option)
      (expr : t) =
    let orig s = fix @@ AbstractExpr.map fst s in
    let rw_alg e =
      let unfold = AbstractExpr.map (fun (e, t) -> (unfix e, t)) e in
      match f unfold with Some e -> e | None -> orig e
    in
    fold_with_type rw_alg expr

  let zero_extend ~n_prefix_bits (e : t) : t =
    unexp ~op:(`ZeroExtend n_prefix_bits) e

  let sign_extend ~n_prefix_bits (e : t) : t =
    unexp ~op:(`SignExtend n_prefix_bits) e

  let extract ~hi_incl ~lo_excl (e : t) : t =
    unexp ~op:(`Extract (hi_incl, lo_excl)) e

  let concat (e : t) (f : t) : t = binexp ~op:`BVConcat e f
  let forall ~bound p = unexp ~op:`Forall (binding bound p)
  let exists ~bound p = unexp ~op:`Exists (binding bound p)
  let boolnot e = unexp ~op:`BoolNOT e
end
